group 'com.luxoft'

version '1.0.1-1-with_events1'

apply plugin: 'application'

sourceCompatibility = 1.8

mainClassName = 'com.luxoft.fabric.Configurator'

repositories {
    mavenCentral()
    maven {
        url "http://172.26.147.116:8081/repository/luxoft.blockchainlab/"
    }
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

dependencies {

//    use local version of `fabric-sdk-java`
    compile project('fabric-sdk-java')
//    compile(group: 'org.hyperledger.fabric-sdk-java', name: 'fabric-sdk-java', version: '1.0.1') {
//        exclude(group: 'junit', module: 'junit')
//    }
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.9'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.9'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.8.9'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    compile group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '5.0.4'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// replace "io.netty:netty-tcnative-boringssl-static" with "com.luxoft:netty-tcnative-luxoft-openssl-static"

configurations.all {
    resolutionStrategy {
        // replace a specific lib explicitly:
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'io.netty' && details.requested.name == 'netty-tcnative-boringssl-static') {
                details.useTarget "com.luxoft:netty-tcnative-luxoft-openssl-static:${details.requested.version}"
            }
        }
    }
}


shadowJar {
    classifier = 'fat'
    archiveName = "${baseName}-${classifier}.${extension}"
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

apply plugin: 'maven-publish'
publishing {
    repositories {
        maven {
            url "http://172.26.147.116:8081/repository/luxoft.blockchainlab/"
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
